plugins {
    id 'java'
    id 'org.springframework.boot' version '4.0.1'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'com.diffplug.spotless' version '8.1.0'
}

group = 'bojanstipic'
version = '0.1.0'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

repositories {
    mavenCentral()
}

dependencyManagement {
    imports {
        mavenBom 'io.opentelemetry.instrumentation:opentelemetry-instrumentation-bom:2.23.0'
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-webmvc'
    implementation 'org.springframework.boot:spring-boot-starter-restclient'
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    runtimeOnly 'org.postgresql:postgresql'
    implementation 'org.springframework.boot:spring-boot-starter-flyway'
    implementation 'org.flywaydb:flyway-database-postgresql'

    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'org.springframework.session:spring-session-data-redis'

    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'io.opentelemetry.instrumentation:opentelemetry-spring-boot-starter'

    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:3.0.1'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation 'org.springframework.boot:spring-boot-starter-webmvc-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-restclient-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-data-jpa-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-security-test'
    testImplementation 'org.springframework.boot:spring-boot-testcontainers'
    testImplementation 'org.testcontainers:testcontainers-junit-jupiter'
    testImplementation 'org.testcontainers:testcontainers-postgresql'
    testImplementation 'com.redis:testcontainers-redis'
}

tasks.named('test') {
    useJUnitPlatform()
}

spotless {
    groovyGradle {
        greclipse().configProperties("""
            org.eclipse.jdt.core.formatter.tabulation.char=space
        """)
    }
    java {
        removeUnusedImports()
        importOrder()
        prettier(['prettier': '3.7.4', 'prettier-plugin-java': '2.7.7'])
        .config([
            'tabWidth': 4,
            'plugins': ['prettier-plugin-java'],
        ])
    }
    sql {
        target 'src/**/*.sql'
        prettier(['prettier': '3.7.4', 'prettier-plugin-sql': '0.19.2'])
        .config([
            'tabWidth': 4,
            'plugins': ['prettier-plugin-sql'],
            'language': 'postgresql',
            'keywordCase': 'lower',
            'dataTypeCase': 'lower',
            'functionCase': 'lower',
            'identifierCase': 'lower',
        ])
    }
}
